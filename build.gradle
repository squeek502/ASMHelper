buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}

apply plugin: 'java'

group = "squeek.asmhelper"

dependencies.add("testCompile", "junit:junit:4.11")
test.testLogging { events "skipped", "failed", "passed" }
sourceSets.test.java.srcDirs += 'tests'
test.exclude "**squeek/asmhelper/TestObfHelper.class"

if (rootProject != project)
{
	rootProject.configurations.minecraft.allDependencies.each { dependencies.add("compile", it) }
	rootProject.configurations.minecraftDeps.allDependencies.each { dependencies.add("compile", it) }
	sourceSets.test.java.srcDirs += 'raw'

	def generator = { String alphabet, int n ->
		new Random().with {
			(1..n).collect { alphabet[ nextInt( alphabet.length() ) ] }.join()
		}
	}
	def pkgToPath = { String pkg -> pkg.tokenize('.').join('/') }
	def pathToPkg = { String path -> path.tokenize('/').join('.') }

	def rawJavaDir = "raw"
	def finalJavaDir = "gen"
	def appendedGroup = rootProject.group != null && !rootProject.group.isEmpty() ? rootProject.group : generator( ('a'..'z').join(), 9 )
	def srcDirPath = project.projectDir.getPath() + "/" + rawJavaDir
	def destDirPath = project.projectDir.getPath() + "/" + finalJavaDir
	def asmhelperPackage = project.group

	def moveAndAlterPackage = { String oldDir, String newDir, String oldPackage, String newPackage ->
		def searchDir = file(oldDir)
		def rootDestDir = file(newDir)

		// clean
		if (rootDestDir.exists())
			rootDestDir.deleteDir()

		def destDir = file(rootDestDir.getPath() + "/" + pkgToPath(newPackage))
		def srcTree = fileTree(dir: searchDir, include: '**/*.java')
		def filesToDelete = []
		def filesToAdd = []
		srcTree.each { file -> 
			def updatedContent = file.getText('UTF-8')
			updatedContent = updatedContent.replaceAll("package " + oldPackage, "package " + newPackage)
			updatedContent = updatedContent.replaceAll("import " + oldPackage, "import " + newPackage)
			def updatedFile = new File(destDir, file.getName())
			filesToAdd += [ file: updatedFile, content: updatedContent ]
			logger.lifecycle("Processed " + file.getPath().replace(project.projectDir.getPath() + File.separator, "") + ", copying to " + updatedFile.getPath().replace(project.projectDir.getPath() + File.separator, ""))
		}
		filesToAdd.each { fileToAdd -> 
			if (!fileToAdd.file.getParentFile().exists())
				fileToAdd.file.getParentFile().mkdirs()
			fileToAdd.file.write(fileToAdd.content, 'UTF-8')
		}
	}

	task generateProjectSpecificPackage() << {
		moveAndAlterPackage(srcDirPath, destDirPath, asmhelperPackage, asmhelperPackage + "." + appendedGroup)
	}
	project.tasks.compileJava.dependsOn(generateProjectSpecificPackage)

	task copyToRootBuildPath(type: Copy, dependsOn: generateProjectSpecificPackage) {
		from destDirPath
		into rootProject.buildDir.getPath()+"/sources/java"
	}

	project.sourceSets.main.java.srcDirs += destDirPath
	rootProject.tasks.sourceMainJava.dependsOn(generateProjectSpecificPackage)
	rootProject.tasks.eclipseClasspath.dependsOn(generateProjectSpecificPackage)
	rootProject.tasks.ideaModule.dependsOn(generateProjectSpecificPackage)
	rootProject.tasks.sourceMainJava.finalizedBy(copyToRootBuildPath)

	task reincorporate() << {
		moveAndAlterPackage(destDirPath, srcDirPath, asmhelperPackage + "." + appendedGroup, asmhelperPackage)
	}
}
else
{
	apply plugin: 'forge'
	minecraft {
		version = "1.7.10-10.13.2.1291"
	}

	sourceSets.main.java.srcDirs += 'raw'
}